# The following commands can be used to create asd.png, which contains a nice
# picture of the input as a graph:
#
#   echo 'digraph G {' > asd.txt
#   grep -- '->' sampleinput.txt | awk '{print $1 "->" $5 " [label="$2"]\n"$3"->"$5" [label="$2"]"}' >> asd.txt
#   echo '}' >> asd.txt
#   dot -Tpng -o asd.png asd.txt

import "stdlib/assert.jou"
import "stdlib/str.jou"
import "stdlib/mem.jou"
import "stdlib/ascii.jou"
import "stdlib/io.jou"
import "stdlib/sort.jou"
import "stdlib/list.jou"


# The sum calculator consists of 45 adders and outputs 46 bits z00,z01,...,z45.
# Each adder (except the first and last) consists of the following 5 logic gates.
enum GateType:
    # Inputs: xnn, ynn (e.g. x05 and y05)
    # Result: 1 if inputs are 1,0 or 0,1
    #         0 if inputs are 1,1 or 0,0
    # Purpose: Used for both output and overflow checking
    InputXor

    # Inputs: xnn, ynn
    # Result: 1 if inputs are 1,1
    #         0 if inputs are anything else
    # Purpose: Used to detect overflow
    InputAnd

    # Inputs: result of InputXor, overflow/carry bit from previous level
    # Result: value of znn (the output bit of x+y)
    # Purpose: Calculates the sum of the two numbers
    OutputXor

    # Inputs: result of InputXor, overflow/carry bit from previous level
    # Result: 1 if we overflow when adding bits 1,0 or 0,1 due to carrying
    #         0 if there's no overflow, or the overflow happens due to input 1,1
    # Purpose: Creates intermediate value used when calculating overflow/carry
    OverflowAnd

    # Inputs: result of InputAnd, result of OverflowAnd
    # Result: 1 if we overflow in any way, due to input 1,1 or carrying
    #         0 if there is no overflow
    # Purpose: This is the overflow/carry bit sent to the next adder
    OverflowOr

# Special cases:
#
#   - First adder doesn't need to handle overflow/carry from previous adders,
#     so it only has InputXor and InputAnd. Result of InputXor is used for
#     output and result of InputAnd is used for carry.
#
#   - Last adder's OverflowOr outputs to the last output bit (z45). In other
#     adders, it goes to the next adder's OutputXor and OverflowAnd.


class Gate:
    inputs: byte[4][2]
    output: byte[4]
    gtype: GateType

    # Gate is sus, if something seems to be wrong with it and we should try to
    # swap its output with others.
    sus: bool

    # Temporary value for sorting
    sort_value: int


def compare_gates(g1: Gate**, g2: Gate**) -> int:
    return (**g1).sort_value - (**g2).sort_value


# By design, every gate uses the inputs somehow.
# We can use this to determine a somewhat reasonable ordering for the gates.
#
# Specifically, if x30 and y30 affect a gate, and inputs x31,y31,x32,y32,...
# don't, we assign it number 30, and then sort by affected values.
def sort_gates(gates: Gate**, ngates: int) -> None:
    maxlen = 400
    todo: byte[4][400]
    done: byte[4][400]

    for inputnum = 0; inputnum < 45; inputnum++:
        sprintf(todo[0], "x%02d", inputnum)  # e.g. x06
        sprintf(todo[1], "y%02d", inputnum)  # e.g. y06
        todo_len = 2
        done_len = 0

        while todo_len > 0:
            name: byte[4] = todo[--todo_len]
            assert strlen(name) == 3

            found = False
            for i = 0; i < done_len; i++:
                if strcmp(done[i], name) == 0:
                    found = True
            if found:
                continue

            for i = 0; i < ngates; i++:
                if strcmp(gates[i].inputs[0], name) == 0 or strcmp(gates[i].inputs[1], name) == 0:
                    gates[i].sort_value = inputnum
                    assert todo_len < maxlen
                    todo[todo_len++] = gates[i].output

            assert done_len < maxlen
            done[done_len++] = name

    Sorter[Gate*]{}.sort(gates, ngates, compare_gates)


# Returns number of errors: 1 if connection is wrong, 0 if it is correct.
# g2g = gate to gate
def check_g2g(from: Gate*, to: Gate*, mark_sus: bool) -> int:
    if strcmp(from.output, to.inputs[0]) == 0 or strcmp(from.output, to.inputs[1]) == 0:
        return 0
    else:
        if mark_sus:
            from.sus = True
            to.sus = True
        return 1


# Returns number of errors: 1 if connection is wrong, 0 if it is correct.
# g2o = gate to output
def check_g2o(from: Gate*, znum: int, mark_sus: bool) -> int:
    znn: byte[10]  # e.g. z05
    sprintf(znn, "z%02d", znum)
    if strcmp(from.output, znn) == 0:
        return 0
    else:
        if mark_sus:
            from.sus = True
        return 1


# Determines how badly the sum machine seems to be wired.
# Zero is a machine that is working as expected.
def count_errors(orig_gates: Gate*, ngates: int, mark_sus: bool) -> int:
    assert ngates <= 250
    gateptrs: Gate*[250]
    for i = 0; i < ngates; i++:
        gateptrs[i] = &orig_gates[i]

    sort_gates(gateptrs, ngates)

    gate_arrays: Gate*[45][5]
    counts = [0, 0, 0, 0, 0]
    for i = 0; i < ngates; i++:
        type_index = gateptrs[i].gtype as int
        assert counts[type_index] < 45
        gate_arrays[type_index][counts[type_index]++] = gateptrs[i]

    # With real input, there are 45 adders, but the first adder is simpler than others.
    # It only has two gates: InputXor for output, InputAnd for overflow/carry.
    num_adders = ngates/5 + 1
    assert counts[GateType.InputXor as int] == num_adders
    assert counts[GateType.InputAnd as int] == num_adders
    assert counts[GateType.OutputXor as int] == num_adders - 1
    assert counts[GateType.OverflowAnd as int] == num_adders - 1
    assert counts[GateType.OverflowOr as int] == num_adders - 1

    ixors: Gate** = gate_arrays[GateType.InputXor as int]
    iands: Gate** = gate_arrays[GateType.InputAnd as int]
    oxors: Gate** = gate_arrays[GateType.OutputXor as int]
    oands: Gate** = gate_arrays[GateType.OverflowAnd as int]
    oors: Gate** = gate_arrays[GateType.OverflowOr as int]

    error_count = 0

    # First adder: InputXor for output
    error_count += check_g2o(ixors[0], 0, mark_sus)

    # First adder: carry into next OutputXor and OverflowAnd
    error_count += check_g2g(iands[0], oxors[0], mark_sus)
    error_count += check_g2g(iands[0], oands[0], mark_sus)

    for i = 1; i < num_adders; i++:
        # Remaining adders: InputXor goes to OutputXor and OverflowAnd
        error_count += check_g2g(ixors[i], oxors[i-1], mark_sus)
        error_count += check_g2g(ixors[i], oands[i-1], mark_sus)

        # Remaining adders: InputAnd goes to OverflowOr
        error_count += check_g2g(iands[i], oors[i-1], mark_sus)

        # Remaining adders: OutputXor computes the result
        error_count += check_g2o(oxors[i-1], i, mark_sus)

        # Remaining adders: OverflowAnd goes to OverflowOr
        error_count += check_g2g(oands[i-1], oors[i-1], mark_sus)

        if i != num_adders-1:
            # Remaining adders except last: OverflowOr goes to next adder's
            # OutputXor and OverflowAnd
            error_count += check_g2g(oors[i-1], oxors[i], mark_sus)
            error_count += check_g2g(oors[i-1], oands[i], mark_sus)

    # Last adder: OverflowOr goes to last output (z45 with real input)
    error_count += check_g2o(oors[num_adders-2], num_adders, mark_sus)

    return error_count


def read_gates(filename: byte*) -> List[Gate]:
    result = List[Gate]{}

    f = fopen(filename, "r")
    assert f != NULL

    line: byte[100]
    while fgets(line, sizeof(line), f) != NULL:
        # Remove comments, if any. I wrote test file by hand.
        if strstr(line, "#") != NULL:
            *strstr(line, "#") = '\0'

        trim_ascii_whitespace(line)

        # ignore blanks and comment-only lines
        if line[0] == '\0':
            continue

        # AoC input files have unnecessary lines at start, e.g. "x01: 1"
        if strstr(line, ":") != NULL:
            continue

        g: Gate
        op: byte[4]
        n = sscanf(line, "%3s %3s %3s -> %3s", &g.inputs[0], op, &g.inputs[1], &g.output)
        assert n == 4

        # Inputs that don't come from other gates cannot be connected wrong, so
        # the wiring mistakes don't mess this up.
        if (
            (g.inputs[0][0] == 'x' and g.inputs[1][0] == 'y')
            or (g.inputs[0][0] == 'y' and g.inputs[1][0] == 'x')
        ):
            # Handles input directly, so it must be InputXor or InputAnd
            match op with strcmp:
                case "XOR":
                    g.gtype = GateType.InputXor
                case "AND":
                    g.gtype = GateType.InputAnd
                case _:
                    assert False
        else:
            match op with strcmp:
                case "XOR":
                    g.gtype = GateType.OutputXor
                case "AND":
                    g.gtype = GateType.OverflowAnd
                case "OR":
                    g.gtype = GateType.OverflowOr
                case _:
                    assert False

        result.append(g)

    fclose(f)
    return result


# Ensures that counting errors returns 0 when there are no errors.
def test_counting() -> None:
    gates = read_gates("sampleinput2-correct.txt")
    assert count_errors(gates.ptr, gates.len as int, False) == 0
    free(gates.ptr)


def fix_order_of_outputs(gates: Gate*, ngates: int) -> None:
    while True:
        for i = 0; i < ngates; i++:
            gates[i].sus = False
        num_errors = count_errors(gates, ngates, True)

        if num_errors == 0:
            break

        nsus = 0
        for i = 0; i < ngates; i++:
            if gates[i].sus:
                nsus++

        # Swap two gates so that error count decreases as much as possible
        best_g1: Gate* = NULL
        best_g2: Gate* = NULL
        best_count = -1

        for g1 = &gates[0]; g1 < &gates[ngates]; g1++:
            if not g1.sus:
                continue

            for g2 = &g1[1]; g2 < &gates[ngates]; g2++:
                if not g2.sus:
                    continue

                memswap(&g1.output, &g2.output, sizeof(g1.output))
                count = count_errors(gates, ngates, False)
                memswap(&g1.output, &g2.output, sizeof(g1.output))

                if best_count == -1 or count < best_count:
                    best_count = count
                    best_g1 = g1
                    best_g2 = g2

        assert best_g1 != NULL
        assert best_g2 != NULL
        memswap(&best_g1.output, &best_g2.output, sizeof(best_g1.output))


def main() -> int:
    test_counting()

    orig_gates = read_gates("sampleinput2-mixed.txt")

    fixed_gates = List[Gate]{}
    fixed_gates.extend(orig_gates)
    fix_order_of_outputs(fixed_gates.ptr, fixed_gates.len as int)

    bad_outputs: byte*[10]
    bad_outputs_len = 0
    assert orig_gates.len == fixed_gates.len
    for i = 0; i < orig_gates.len; i++:
        if strcmp(orig_gates.ptr[i].output, fixed_gates.ptr[i].output) != 0:
            assert bad_outputs_len < 10
            bad_outputs[bad_outputs_len++] = orig_gates.ptr[i].output

    # Output: 4 gates were in the wrong place (2 swaps)
    printf("%d gates were in the wrong place (%d swaps)\n", bad_outputs_len, bad_outputs_len / 2)

    # Output: ia1,ia2,ix2,ix3
    sort_strings(bad_outputs, bad_outputs_len)
    for i = 0; i < bad_outputs_len; i++:
        if i != 0:
            printf(",")
        printf("%s", bad_outputs[i])
    printf("\n")

    free(orig_gates.ptr)
    free(fixed_gates.ptr)
    return 0
