import "stdlib/assert.jou"
import "stdlib/io.jou"
import "stdlib/mem.jou"
import "stdlib/str.jou"


def main() -> int:
    f = fopen("sampleinput.txt", "r")
    assert f != NULL

    line: byte[256]
    ray_counts: int64[256]
    new_ray_counts: int64[256]

    memset(ray_counts, 0, sizeof(ray_counts))

    while fgets(line, sizeof(line) as int, f) != NULL:
        memset(new_ray_counts, 0, sizeof(new_ray_counts))
        if strstr(line, "S") != NULL:
            new_ray_counts[(strstr(line, "S") as int64) - (&line[0] as int64)] = 1
        else:
            for i = 0; line[i] != '\0'; i++:
                if line[i] == '^':
                    assert i >= 1
                    assert i + 1 < array_count(ray_counts)
                    new_ray_counts[i-1] += ray_counts[i]
                    new_ray_counts[i+1] += ray_counts[i]
                else:
                    new_ray_counts[i] += ray_counts[i]

        ray_counts = new_ray_counts

    result: int64 = 0
    for i = 0; i < array_count(ray_counts); i++:
        result += ray_counts[i]
    printf("%lld\n", result)  # Output: 40

    fclose(f)
    return 0
