import "stdlib/assert.jou"
import "stdlib/sort.jou"
import "stdlib/io.jou"
import "stdlib/list.jou"
import "stdlib/mem.jou"

# This needs to be global to sort so we get indexes of top N pairs of closest points
global points: List[int[3]]


def distance_squared(p1: int[3], p2: int[3]) -> int64:
    dx: int64 = p2[0] - p1[0]
    dy: int64 = p2[1] - p1[1]
    dz: int64 = p2[2] - p1[2]
    return dx*dx + dy*dy + dz*dz


def comparator_func(pair1: int[2]*, pair2: int[2]*) -> int:
    dist1_squared = distance_squared(points.ptr[(*pair1)[0]], points.ptr[(*pair1)[1]])
    dist2_squared = distance_squared(points.ptr[(*pair2)[0]], points.ptr[(*pair2)[1]])
    if dist1_squared > dist2_squared:
        return 1
    if dist1_squared < dist2_squared:
        return -1
    return 0


def main() -> int:
    f = fopen("sampleinput.txt", "r")
    assert f != NULL

    x, y, z: int
    while fscanf(f, "%d,%d,%d\n", &x, &y, &z) == 3:
        points.append([x, y, z])
    fclose(f)

    index_pairs = List[int[2]]{}
    for i1 = 0; i1 < points.len; i1++:
        for i2 = i1 + 1; i2 < points.len; i2++:
            index_pairs.append([i1, i2])

    Sorter[int[2]]{}.sort(index_pairs.ptr, index_pairs.len, comparator_func)

    group_ids: int* = malloc(sizeof(group_ids[0]) * points.len)
    assert group_ids != NULL
    for i = 0; i < points.len; i++:
        group_ids[i] = i

    result: int64 = -1

    # Connect all pairs of points if not connected
    for p = index_pairs.ptr; p < index_pairs.end(); p++:
        i1 = (*p)[0]
        i2 = (*p)[1]
        # Bring the points to the same circuit by replacing one ID with another
        old_id = group_ids[i1]
        new_id = group_ids[i2]
        if old_id == new_id:
            continue
        result = (points.ptr[i1][0] as int64) * (points.ptr[i2][0] as int64)
        for k = 0; k < points.len; k++:
            if group_ids[k] == old_id:
                group_ids[k] = new_id

    printf("%lld\n", result)  # Output: 25272

    free(index_pairs.ptr)
    free(group_ids)
    free(points.ptr)
    return 0
