import "stdlib/assert.jou"
import "stdlib/io.jou"
import "stdlib/math.jou"


class Block:
    # start and end are included, same start and end means 1x1x1 block.
    # These must be in the correct order: start[i] <= end[i] for i=0,1,2.
    start: int[3]
    end: int[3]

    if False:
        def print(self) -> None:
            printf("%d,%d,%d~%d,%d,%d\n", self.start[0], self.start[1], self.start[2], self.end[0], self.end[1], self.end[2])

    def intersects(self, other: Block*) -> bool:
        for i = 0; i < 3; i++:
            assert self.start[i] <= self.end[i]
            assert other.start[i] <= other.end[i]

            # Try to find out where the intersection would be, and see if it's empty
            start = max(self.start[i], other.start[i])
            end = min(self.end[i], other.end[i])
            if start > end:
                return False

        return True


def block_can_fall(blocks: Block*, nblocks: int, b: Block*) -> bool:
    assert 0 <= b.start[2] and b.start[2] <= b.end[2]
    if b.start[2] == 0:
        return False

    b.start[2]--
    b.end[2]--

    for i = 0; i < nblocks; i++:
        if &blocks[i] != b and blocks[i].intersects(b):
            b.start[2]++
            b.end[2]++
            return False

    b.start[2]++
    b.end[2]++
    return True


def main() -> int:
    blocks: Block[2000]
    nblocks = 0

    f = fopen("sampleinput.txt", "r")
    assert f != NULL

    b: Block
    while fscanf(f, "%d,%d,%d~%d,%d,%d\n", &b.start[0], &b.start[1], &b.start[2], &b.end[0], &b.end[1], &b.end[2]) == 6:
        assert nblocks < array_count(blocks)
        blocks[nblocks++] = b

    fclose(f)

    for i = 0; i < nblocks; i++:
        for k = 0; k < i; k++:
            assert not blocks[i].intersects(&blocks[k])

    # Move blocks down as much as possible without intersecting
    did_something = True
    while did_something:
        did_something = False
        for i = 0; i < nblocks; i++:
            if block_can_fall(blocks, nblocks, &blocks[i]):
                blocks[i].start[2]--
                blocks[i].end[2]--
                did_something = True

    result = 0

    for i = 0; i < nblocks; i++:
        removed = blocks[i]
        blocks[i] = blocks[--nblocks]

        can_be_removed = True
        for k = 0; k < nblocks; k++:
            if block_can_fall(blocks, nblocks, &blocks[k]):
                can_be_removed = False
                break

        blocks[nblocks++] = blocks[i]
        blocks[i] = removed

        if can_be_removed:
            result++

    printf("%d\n", result)  # Output: 5

    return 0
