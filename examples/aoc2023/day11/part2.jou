import "stdlib/io.jou"
import "stdlib/mem.jou"
import "stdlib/str.jou"
import "../grid.jou"


# returned array is terminated by -1
def find_blank_lines(grid: Grid*) -> int*:
    result: int* = malloc(sizeof(result[0]) * (grid->height + 1))
    result_len = 0

    for y = 0; y < grid->height; y++:
        if strspn(&grid->data[(grid->width + 1)*y], ".") == grid->width:
            result[result_len++] = y
    result[result_len] = -1
    return result


def sum_vertical_distances(grid: Grid*) -> long:
    hashtags = grid->find_all('#')
    blank_lines = find_blank_lines(grid)

    result: long = 0
    for end = hashtags; (*end)[0] != -1 and (*end)[1] != -1; end++:
        for start = hashtags; start < end; start++:
            for y = (*start)[1]; y < (*end)[1]; y++:
                y_is_blank = False
                for p = blank_lines; *p != -1; p++:
                    if *p == y:
                        y_is_blank = True
                        break
                if y_is_blank:
                    # Change to 1000000 for actual input
                    result += 100
                    #result += 1000000
                else:
                    result += 1

    free(hashtags)
    free(blank_lines)
    return result


def main() -> int:
    f = fopen("sampleinput.txt", "r")
    assert f != NULL
    grid = read_grid_from_file(f)
    fclose(f)

    result = sum_vertical_distances(&grid)
    grid.transpose()
    result += sum_vertical_distances(&grid)
    printf("%lld\n", result)  # Output: 8410

    free(grid.data)
    return 0
