import "stdlib/str.jou"
import "stdlib/list.jou"
import "stdlib/io.jou"
import "stdlib/mem.jou"


def substitute_questionmarks_in_all_ways(questional_string: byte*) -> List[byte[100]]:
    assert strlen(questional_string) == strspn(questional_string, "?.#")
    assert 0 < strlen(questional_string) and strlen(questional_string) < 100
    temp: byte[100]

    for i = 0; questional_string[i] != '\0'; i++:
        if questional_string[i] == '?':
            strcpy(temp, questional_string)

            temp[i] = '#'
            with_hashtag = substitute_questionmarks_in_all_ways(temp)
            temp[i] = '.'
            without_hashtag = substitute_questionmarks_in_all_ways(temp)

            result = with_hashtag
            result.extend(without_hashtag)
            free(without_hashtag.ptr)
            return result

    strcpy(temp, questional_string)
    result = List[byte[100]]{}
    result.append(temp)
    return result


# "##..#..###" --> "2,1,3"
def generate_numbers(non_questional_string: byte*) -> byte[100]:
    assert strlen(non_questional_string) < 50
    result: byte[100] = ""

    prev = '.'
    n = 0
    for p = non_questional_string; *p != '\0'; p++:
        assert *p == '#' or *p == '.'
        if prev == '.' and *p == '#':
            n = 1
        elif prev == '#' and *p == '#':
            n++
        elif prev == '#' and *p == '.':
            sprintf(&result[strlen(result)], "%d,", n)
        prev = *p

    if prev == '#':
        sprintf(&result[strlen(result)], "%d,", n)

    if result[0] != '\0':
        # remove last comma
        result[strlen(result) - 1] = '\0'
    return result


def main() -> int:
    f = fopen("sampleinput.txt", "r")
    assert f != NULL

    questional_string: byte[100]
    non_questional_string: byte[100]

    result = 0

    while fscanf(f, "%99s %99s\n", questional_string, non_questional_string) == 2:
        #puts(questional_string)
        possibilities = substitute_questionmarks_in_all_ways(questional_string)
        for p = possibilities.ptr; p < possibilities.end(); p++:
            s = generate_numbers(*p)
            if strcmp(s, non_questional_string) == 0:
                result++
        free(possibilities.ptr)

    fclose(f)
    printf("%d\n", result)  # Output: 21
    return 0
