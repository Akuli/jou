import "stdlib/str.jou"
import "stdlib/mem.jou"

import "./ast.jou"
import "./errors_and_warnings.jou"


@public
def evaluate_array_length(expr: AstExpression*) -> int:
    if expr->kind == AstExpressionKind.Int:
        return expr->int_value
    fail(expr->location, "cannot evaluate array length at compile time")


@public
def get_special_constant(name: byte*) -> int:
    match name with strcmp:
        case "WINDOWS":
            return WINDOWS as int
        case "MACOS":
            return MACOS as int
        case "NETBSD":
            return NETBSD as int
        case _:
            return -1


def evaluate_condition(expr: AstExpression*) -> int:
    match expr->kind:
        case AstExpressionKind.GetVariable:
            return get_special_constant(expr->varname)
        case AstExpressionKind.And:
            match evaluate_condition(&expr->operands[0]):
                case 1:
                    return evaluate_condition(&expr->operands[1])
                case 0:
                    return 0  # left side false, result false so don't evaluate right side
                case -1:
                    return -1
                case _:
                    assert False
        case AstExpressionKind.Or:
            match evaluate_condition(&expr->operands[0]):
                case 1:
                    return 1  # left side true, result true so don't evaluate right side
                case 0:
                    return evaluate_condition(&expr->operands[1])
                case -1:
                    return -1  # error
                case _:
                    assert False
        case AstExpressionKind.Not:
            match evaluate_condition(&expr->operands[0]):
                case 1:
                    return 0
                case 0:
                    return 1
                case -1:
                    return -1
                case _:
                    assert False
        case _:
            return -1


# returns the statements to replace if statement with, as a pointer inside if_stmt
def evaluate_if_statement(if_stmt: AstIfStatement*) -> AstBody*:
    for i = 0; i < if_stmt->n_if_and_elifs; i++:
        match evaluate_condition(&if_stmt->if_and_elifs[i].condition):
            case -1:
                # don't know how to evaluate it
                return NULL
            case 0:
                # try the next elif or else
                pass
            case 1:
                # condition is true, let's use this if or elif
                return &if_stmt->if_and_elifs[i].body
            case _:
                assert False
    return &if_stmt->else_body


# Replace body->statements[i] with zero or more statements from another body.
def replace(body: AstBody*, i: int, new: AstBody) -> None:
    body->statements[i].free()

    item_size = sizeof(body->statements[0])
    body->statements = realloc(body->statements, (body->nstatements + new.nstatements) * item_size)
    memmove(&body->statements[i + new.nstatements], &body->statements[i+1], (body->nstatements - (i+1)) * item_size)
    memcpy(&body->statements[i], new.statements, new.nstatements * item_size)

    free(new.statements)
    body->nstatements--
    body->nstatements += new.nstatements


# This handles nested if statements.
@public
def evaluate_compile_time_if_statements(body: AstBody*) -> None:
    for i = 0; i < body->nstatements; i++:
        match body->statements[i].kind:
            case AstStatementKind.If:
                ptr = evaluate_if_statement(&body->statements[i].if_statement)
                if ptr != NULL:
                    replacement = *ptr
                    *ptr = AstBody{}  # avoid double-free
                    replace(body, i, replacement)
                    i--  # skip same index again to handle nested if statements
            case AstStatementKind.WhileLoop:
                evaluate_compile_time_if_statements(&body->statements[i].while_loop.body)
            case AstStatementKind.ForLoop:
                evaluate_compile_time_if_statements(&body->statements[i].for_loop.body)
            case AstStatementKind.Class:
                pass
            case AstStatementKind.FunctionDef:
                evaluate_compile_time_if_statements(&body->statements[i].function.body)
            case (
                AstStatementKind.ExpressionStatement
                | AstStatementKind.Assert
                | AstStatementKind.Pass
                | AstStatementKind.Return
                | AstStatementKind.Match
                | AstStatementKind.Break
                | AstStatementKind.Continue
                | AstStatementKind.DeclareLocalVar
                | AstStatementKind.Assign
                | AstStatementKind.InPlaceAdd
                | AstStatementKind.InPlaceSub
                | AstStatementKind.InPlaceMul
                | AstStatementKind.InPlaceDiv
                | AstStatementKind.InPlaceMod
                | AstStatementKind.FunctionDeclare
                | AstStatementKind.Enum
                | AstStatementKind.GlobalVariableDeclare
                | AstStatementKind.GlobalVariableDef
            ):
                # these statements cannot contain if statements, no need to recurse inside
                pass
