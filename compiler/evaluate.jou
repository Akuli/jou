import "stdlib/str.jou"
import "stdlib/mem.jou"

import "./ast.jou"
import "./errors_and_warnings.jou"


def evaluate_array_length(expr: AstExpression*) -> int:
    if expr->kind == AstExpressionKind.Int:
        return expr->int_value
    fail(expr->location, "cannot evaluate array length at compile time")


def get_special_constant(name: byte*) -> int:
    if strcmp(name, "WINDOWS") == 0:
        return WINDOWS as int
    if strcmp(name, "MACOS") == 0:
        return MACOS as int
    if strcmp(name, "NETBSD") == 0:
        return NETBSD as int
    return -1


def evaluate_condition(expr: AstExpression*) -> bool:
    match expr->kind:
        case AstExpressionKind.GetVariable:
            v = get_special_constant(expr->varname)
            if v == 0:
                return False
            if v == 1:
                return True
            fail(expr->location, "cannot evaluate condition at compile time")
        case AstExpressionKind.And:
            return evaluate_condition(&expr->operands[0]) and evaluate_condition(&expr->operands[1])
        case AstExpressionKind.Or:
            return evaluate_condition(&expr->operands[0]) or evaluate_condition(&expr->operands[1])
        case AstExpressionKind.Not:
            return not evaluate_condition(&expr->operands[0])
        case _:
            fail(expr->location, "cannot evaluate condition at compile time")


# returns the statements to replace if statement with
def evaluate_compile_time_if_statement(if_stmt: AstIfStatement*) -> AstBody:
    result = &if_stmt->else_body
    for i = 0; i < if_stmt->n_if_and_elifs; i++:
        if evaluate_condition(&if_stmt->if_and_elifs[i].condition):
            result = &if_stmt->if_and_elifs[i].body
            break

    ret = *result
    *result = AstBody{}  # avoid double-free
    return ret


# Replace body->statements[i] with zero or more statements from another body.
def replace(body: AstBody*, i: int, new: AstBody) -> None:
    body->statements[i].free()

    item_size = sizeof(body->statements[0])
    body->statements = realloc(body->statements, (body->nstatements + new.nstatements) * item_size)
    memmove(&body->statements[i + new.nstatements], &body->statements[i+1], (body->nstatements - (i+1)) * item_size)
    memcpy(&body->statements[i], new.statements, new.nstatements * item_size)

    free(new.statements)
    body->nstatements--
    body->nstatements += new.nstatements


# This handles nested if statements.
def evaluate_compile_time_if_statements(body: AstBody*) -> None:
    i = 0
    while i < body->nstatements:
        if body->statements[i].kind == AstStatementKind.If:
            replace(body, i, evaluate_compile_time_if_statement(&body->statements[i].if_statement))
        else:
            i++
