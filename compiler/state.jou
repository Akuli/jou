import "stdlib/list.jou"
import "stdlib/mem.jou"

import "./ast.jou"
import "./constants.jou"
import "./command_line_args.jou"
import "./errors_and_warnings.jou"
import "./hash.jou"
import "./target.jou"
import "./types.jou"


@public
class Imported:
    # The statement that defines the thing, e.g. public function definition in other file
    statement_in_other_file: AstStatement*

    # Import statement in AST of the file that imports
    the_import: AstImport*

    # Location of the import statement
    import_location: Location


# Jou source code file (.jou)
@public
class JouFile:
    path: byte*  # owned, always set, unique
    is_main_file: bool  # is this the file passed in Jou compiler command? False means imported file
    ast: List[AstStatement]

    # Points to @public things in ASTS of other files that have been imported
    # into this file. Used when figuring out whether something comes in from
    # another file.
    imported_from_other_files: List[Imported]

    def free(self) -> None:
        for p = self.ast.ptr; p < self.ast.end(); p++:
            p.free()
        free(self.ast.ptr)
        free(self.path)
        free(self.imported_from_other_files.ptr)


@public
class SpecialConstant:
    name: byte*
    constant: Constant


@public
class CompilerState:
    args: CommandLineArgs
    stdlib_path: byte*  # owned
    types: TypeState
    warnings_shown: bool  # for the --fail-on-warnings compiler flag
    cached_compiler_hash: Hash

    special_constants: SpecialConstant[3]

    # Appending to this list happens only in the early stages of compilation,
    # so for most purposes, pointers into this list are stable and won't ever
    # become invalid.
    jou_files: List[JouFile]

    # Initialized somewhat late during compilation, not needed for a lot of things
    target: Target

    # File content is loaded here when tokenizing begins, and it stays here
    # until tokenizing the next file. We parse immediately after tokenizing, so
    # this way the parser can access the source code that is still in memory.

    # This method exists only to make valgrind happy. It is called only when
    # the compiler is exiting and the operating system is about to free all
    # memory we used anyway.
    def free(self) -> None:
        free(self.stdlib_path)
        for p = self.jou_files.ptr; p < self.jou_files.end(); p++:
            p.free()
        free(self.jou_files.ptr)
        self.types.free()


# Yes, I know, everyone hates global variables, especially public global variables...
#
# For now, there wouldn't be much advantage in passing this around.
# Can be cleaned up later if needed.
@public
global global_compiler_state: CompilerState
