# Second step of type checking is to go through bodies of classes and populate
# the corresponding type objects. However, we don't look inside bodies of
# methods yet; we only handle the fields (e.g. "x: int" inside a class).
#
# Methods are completely ignored at this stage. They are treated like functions.

import "stdlib/assert.jou"
import "stdlib/io.jou"
import "stdlib/list.jou"

import "../ast.jou"
import "../state.jou"
import "../types.jou"
import "./common.jou"


def handle_class_members(jou_file: JouFile*, classdef: AstClassDef*) -> None:
    # Previous type-checking step created an empty class.
    t = classdef.symbol.type
    assert t != NULL
    assert t.kind == TypeKind.Class

    union_id = 0
    for stmt = classdef.body.ptr; stmt < classdef.body.end(); stmt++:
        match stmt.kind:
            case AstStatementKind.ClassField:
                f = ClassField{
                    name = stmt.class_field.name,
                    type = type_from_ast(jou_file, t, &stmt.class_field.type),
                    union_id = union_id++,
                }
                t.classdata.fields.append(f)

            case AstStatementKind.ClassUnion:
                uid = union_id++
                for ntv = stmt.union_fields.ptr; ntv < stmt.union_fields.end(); ntv++:
                    f = ClassField{
                        name = ntv.name,
                        type = type_from_ast(jou_file, t, &ntv.type),
                        union_id = uid,
                    }
                    t.classdata.fields.append(f)

            case AstStatementKind.Pass:
                pass

            case AstStatementKind.MethodDef:
                # Checking is done elsewhere to treat methods similarly to functions.
                pass

            case _:
                assert False

    if t.classdata.is_generic():
        t.update_fields_to_generic_instances()


@public
def typecheck_step2_class_members() -> None:
    if global_compiler_state.args.verbosity >= 1:
        printf("Typecheck: class bodies (step 2/3)\n")

    for jou_file = global_compiler_state.jou_files.ptr; jou_file < global_compiler_state.jou_files.end(); jou_file++:
        if global_compiler_state.args.verbosity >= 1:
            printf("  %s\n", jou_file.path)

        for stmt = jou_file.ast.ptr; stmt < jou_file.ast.end(); stmt++:
            if stmt.kind == AstStatementKind.Class:
                if global_compiler_state.args.verbosity >= 2:
                    printf("    class %s\n", stmt.symbol().name)
                handle_class_members(jou_file, &stmt.classdef)
