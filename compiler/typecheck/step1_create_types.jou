# First step of type checking is to create types, so that they exist when later
# stages reference the types.
#
# After the first step, classes defined in Jou exist, but they are all opaque
# and contain no members. This way, when step 2 checks function signatures,
# they can refer to classes that are defined anywhere in the project, e.g.
# later in the same file.
#
# Enums are simple. The first step creates them and also fills in their members,
# although it doesn't really matter whether that's done in step 1 or 2.

import "stdlib/str.jou"
import "stdlib/mem.jou"

import "../ast.jou"
import "../errors_and_warnings.jou"
import "../types.jou"
import "./common.jou"


@public
def typecheck_step1_create_types(ast: AstFile*) -> ExportSymbol*:
    exports: ExportSymbol* = NULL
    nexports = 0

    for i = 0; i < ast->body.nstatements; i++:
        stmt = &ast->body.statements[i]

        match stmt->kind:
            case AstStatementKind.Class:
                name = stmt->classdef.name
                public = stmt->classdef.public
                usedptr = &stmt->classdef.used
                t = create_opaque_class(name)
                stmt->classdef.type = t
            case AstStatementKind.Enum:
                name = stmt->enumdef.name
                public = stmt->enumdef.public
                usedptr = &stmt->enumdef.used
                t = create_enum(name, stmt->enumdef.member_count, stmt->enumdef.member_names)
                stmt->enumdef.type = t
            case _:
                continue

        existing = ast->types.find_type(name)
        if existing != NULL:
            msg: byte[500]
            snprintf(msg, sizeof(msg), "%s named '%s' already exists", existing->short_description(), name)
            fail(stmt->location, msg)

        ast->types.add_type(t, usedptr)

        if public:
            exports = realloc(exports, sizeof(exports[0]) * (nexports+1))
            assert exports != NULL
            exports[nexports++] = ExportSymbol{kind = ExportSymbolKind.Type, type = t, name = name}

    exports = realloc(exports, sizeof(exports[0]) * (nexports + 1))
    assert exports != NULL
    exports[nexports] = ExportSymbol{}  # list terminator
    return exports
