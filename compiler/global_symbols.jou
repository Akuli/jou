# A global symbol is anything that can be referred by name anywhere in a file,
# e.g. a class, a function or a global constant. If a symbol is decorated with
# @public, it is also accessible in other files.
#
# The Jou compiler does not allocate memory for symbols separately. Instead,
# the symbols are simply placed inside the AST statement classes.

import "stdlib/assert.jou"
import "stdlib/str.jou"

import "./ast.jou"
import "./state.jou"
import "./types.jou"
import "./constants.jou"


@public
enum GlobalSymbolKind:
    Function
    Type
    GenericClass  # similar to Type, contains something like List[T] where T is a type variable
    GlobalVar
    Const

@public
enum GlobalSymbolStatus:
    NotTypeChecked  # Type checking not started yet (the default, must be first)
    TypeChecking    # We are currently type checking this. Used to detect self-referencing things.
    Ready           # Type checked, e.g. we now know the value of a constant or members of a class


@public
class GlobalSymbol:
    name: byte[100]
    kind: GlobalSymbolKind
    status: GlobalSymbolStatus
    public: bool  # Is it decorated with @public?
    used: bool    # If not @public, this is used to show unused warnings

    union:
        # These are zero initialized at first, populated during type checking.
        funcsignature: Signature  # GlobalSymbolKind.Function
        type: Type*  # GlobalSymbolKind.Type, GlobalSymbolKind.GlobalVar, GlobalSymbolKind.GenericClass
        constant: Constant  # GlobalSymbolKind.Const

    def free(self) -> None:
        match self.kind:
            case GlobalSymbolKind.Function:
                self.funcsignature.free()
            case GlobalSymbolKind.Const:
                self.constant.free()
            case _:
                pass

    def short_description(self) -> byte*:
        match self.kind:
            case GlobalSymbolKind.Function:
                return "function"
            case GlobalSymbolKind.GlobalVar:
                return "global variable"
            case GlobalSymbolKind.Type:
                return "type"
            case GlobalSymbolKind.GenericClass:
                return "generic class"
            case GlobalSymbolKind.Const:
                return "constant"


# If you have a Jou file that has access to printf() function from stdlib/io.jou,
# then this function returns the `declare` statement in stdlib/io.jou when you
# call this with the file and the string "printf".
#
# This also finds things defined in the same file.
#
# Note: For special constants like "WINDOWS" this returns NULL, be aware!!!
@public
def find_global_symbol_statement(jou_file: JouFile*, name: byte*, mark_import_as_used: bool) -> AstStatement*:
    # Defined in this file?
    for stmt = jou_file.ast.ptr; stmt < jou_file.ast.end(); stmt++:
        if stmt.symbol() != NULL and strcmp(stmt.symbol().name, name) == 0:
            return stmt

    # Imported?
    for imported = jou_file.imported_from_other_files.ptr; imported < jou_file.imported_from_other_files.end(); imported++:
        assert imported.statement_in_other_file.symbol() != NULL
        if strcmp(imported.statement_in_other_file.symbol().name, name) == 0:
            if mark_import_as_used:
                imported.the_import.used = True
            return imported.statement_in_other_file

    return NULL


# Note: For special constants like "WINDOWS" this returns NULL, be aware!!!
@public
def find_global_symbol(jou_file: JouFile*, name: byte*, mark_import_as_used: bool) -> GlobalSymbol*:
    stmt = find_global_symbol_statement(jou_file, name, mark_import_as_used)
    if stmt == NULL:
        return NULL
    return stmt.symbol()
