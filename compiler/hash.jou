# This file is used to calculate 64-bit hashes. They are used to check whether
# source files have changed so that we need to recompile.
#
# The hash algorithm we use is FNV-1a:
# https://en.wikipedia.org/wiki/Fowler%E2%80%93Noll%E2%80%93Vo_hash_function
# It is super easy to implement. We use the 64-bit variant to get a reasonably
# low probability for collisions.
#
# Of course, this is not cryptographically secure, but it doesn't matter for a
# compiler: if someone can place whatever they want into `jou_compiled`, you're
# doomed anyway :)

import "stdlib/assert.jou"
import "stdlib/str.jou"


# Do not instantiate this as `Hash{}`, that uses wrong initial value.
# Use new_hash() function.
@public
class Hash:
    hash: uint64
    created_correctly: bool

    # Add data to hash
    def add_byte(self, b: byte) -> None:
        assert self.created_correctly  # If this fails, use new_hash() function
        self.hash ^= b
        self.hash *= 0x100_0000_01b3

    def add_bytes(self, data: byte*, len: int64) -> None:
        for i: int64 = 0; i < len; i++:
            self.add_byte(data[i])

    def add_bool(self, b: bool) -> None:
        self.add_byte(b as byte)

    def add_int(self, i: int) -> None:
        self.add_bytes(&i as byte*, sizeof(i))

    def add_int64(self, i: int64) -> None:
        self.add_bytes(&i as byte*, sizeof(i))

    def add_uint64(self, i: uint64) -> None:
        self.add_bytes(&i as byte*, sizeof(i))

    def add_string(self, s: byte*) -> None:
        len = strlen(s)
        self.add_int64(len)
        self.add_bytes(s, len)


@public
def new_hash() -> Hash:
    return Hash{hash = 0xcbf2_9ce4_8422_2325, created_correctly = True}
