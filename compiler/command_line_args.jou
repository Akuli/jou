import "stdlib/ascii.jou"
import "stdlib/assert.jou"
import "stdlib/io.jou"
import "stdlib/str.jou"
import "stdlib/process.jou"

import "./update.jou"


@public
enum CompilingMode:
    Run             # default mode (must be first)
    CompileToFile   # -o
    Check           # --check
    TokenizeOnly    # --tokenize-only
    ParseOnly       # --parse-only
    UvgOnly         # --uvg-only


@public
class CommandLineArgs:
    mode: CompilingMode

    # Please keep the help printing up to date!
    argv0: byte*            # Program name
    infile: byte*           # The "main" Jou file (can import other files)
    outfile: byte*          # -o
    optlevel: int           # -O0, -O1, -O2, -O3
    parallelism: int        # -j1, -j2, ...
    valgrind: bool          # --valgrind
    fail_on_warnings: bool  # --fail-on-warnings
    linker_flags: byte*     # --linker-flags
    verbosity: int          # -v, -vv etc


def print_help(argv0: byte*) -> None:
    printf("Usage:\n")
    printf("  %s [options] file.jou  # Compile and run code\n", argv0)
    printf("  %s --help              # This message\n", argv0)
    printf("  %s --update            # Download and install the latest Jou\n", argv0)
    printf("\n")
    printf("Options for compiling:\n")
    printf("  -o OUTFILE           output an executable file, don't run the code\n")
    printf("  -O0/-O1/-O2/-O3      set optimization level (0 = no optimization, 1 = default, 3 = runs fastest)\n")
    printf("  --check              do not output or run anything, just check for errors in code\n")
    printf("  --valgrind           use valgrind when running the code\n")
    printf("  --fail-on-warnings   fail compiling if there were any compiler warnings\n")
    printf("  --linker-flags \"...\" appended to the linker command, just like 'link' statements in code\n")
    printf("  -j1/-j2/-j3/...      max number of threads for compiling object files (default = number of CPU cores)\n")
    printf("\n")
    printf("Options for debugging the compiler:\n")
    printf("  -v / --verbose    show briefly what the compiler is doing\n")
    printf("  -vv               show even more details\n")
    printf("  --tokenize-only   display the output of the tokenizer, do not compile further\n")
    printf("  --parse-only      display the AST (parse tree), do not compile further\n")
    printf("  --uvg-only        display Undefined Value Graphs, do not compile further\n")


# Only for showing reasonable error messages
def mode_to_flag(m: CompilingMode) -> byte*:
    match m:
        case CompilingMode.Run:
            return ""  # hopefully this never runs...
        case CompilingMode.CompileToFile:
            return "-o"
        case CompilingMode.Check:
            return "--check"
        case CompilingMode.TokenizeOnly:
            return "--tokenize-only"
        case CompilingMode.ParseOnly:
            return "--parse-only"
        case CompilingMode.UvgOnly:
            return "--uvg-only"


def set_mode(args: CommandLineArgs*, new_mode: CompilingMode) -> None:
    assert new_mode != CompilingMode.Run
    if args.mode != CompilingMode.Run:
        # A mode has already been set explicitly.
        if args.mode == new_mode:
            fprintf(
                get_stderr(),
                "%s: %s cannot be given multiple times (try \"%s --help\")\n",
                args.argv0, mode_to_flag(new_mode), args.argv0,
            )
        else:
            fprintf(
                get_stderr(),
                "%s: %s and %s cannot be used together (try \"%s --help\")\n",
                args.argv0, mode_to_flag(args.mode), mode_to_flag(new_mode), args.argv0,
            )
        exit(2)
    args.mode = new_mode


def warn_about_meaningless_arg(args: CommandLineArgs*, the_arg: byte*) -> None:
    fprintf(
        get_stderr(),
        "%s: warning: %s does nothing when used together with %s (see \"%s --help\")\n",
        args.argv0, the_arg, mode_to_flag(args.mode), args.argv0,
    )
    fflush(get_stderr())


# Must be done after setting mode, because this depends on the mode
def check_args(args: CommandLineArgs*, optlevel_set: bool) -> None:
    # -O0, -O1 etc don't make sense if we won't actually compile the code
    if optlevel_set and args.mode != CompilingMode.Run and args.mode != CompilingMode.CompileToFile:
        o_flag: byte[10]
        sprintf(o_flag, "-O%d", args.optlevel)
        warn_about_meaningless_arg(args, o_flag)

    # valgrinding doesn't make sense if we won't run the code :)
    if args.valgrind and args.mode != CompilingMode.Run:
        warn_about_meaningless_arg(args, "--valgrind")

    # linker flags don't make sense if there's nothing to link
    if args.linker_flags != NULL and args.mode != CompilingMode.Run and args.mode != CompilingMode.CompileToFile:
        warn_about_meaningless_arg(args, "--linker-flags")

    # Parallelism doesn't make sense if we won't invoke LLVM, because only LLVM
    # stuff is parallelized.
    #
    # TODO: test this, and test the -j flag in general...
    if args.parallelism != 0 and args.mode != CompilingMode.Run and args.mode != CompilingMode.CompileToFile:
        flag: byte[100]
        sprintf(flag, "-j%d", args.parallelism)
        warn_about_meaningless_arg(args, flag)


@public
def parse_command_line_args(argc: int, argv: byte**) -> CommandLineArgs:
    # Set default optimize to O1, user sets optimize will overwrite the default flag
    args = CommandLineArgs{argv0 = argv[0], optlevel = 1}

    if argc == 2:
        match argv[1] with strcmp:
            case "--help":
                print_help(argv[0])
                exit(0)
            case "--update":
                update_jou_compiler()
                exit(0)

    optlevel_set = False
    i = 1
    while i < argc:
        match argv[i] with strcmp:
            case "--help" | "--update":
                fprintf(get_stderr(), "%s: \"%s\" cannot be used with other arguments (try \"%s --help\")\n", argv[0], argv[i], argv[0])
                exit(2)

            # Options for compiling
            case "-o":
                if argc-i < 2:
                    fprintf(get_stderr(), "%s: there must be a file name after -o (try \"%s --help\")\n", argv[0], argv[0])
                    exit(2)
                set_mode(&args, CompilingMode.CompileToFile)
                args.outfile = argv[i+1]
                if strlen(args.outfile) > 4 and ends_with(args.outfile, ".jou"):
                    fprintf(get_stderr(), "%s: the filename after -o should be an executable, not a Jou file (try \"%s --help\")\n", argv[0], argv[0])
                    exit(2)
                i += 2
            case "-O0" | "-O1" | "-O2" | "-O3":
                args.optlevel = argv[i][2] - '0'
                optlevel_set = True
                i++
            case "--check":
                set_mode(&args, CompilingMode.Check)
                i++
            case "--valgrind":
                args.valgrind = True
                i++
            case "--fail-on-warnings":
                args.fail_on_warnings = True
                i++
            case "--linker-flags":
                if args.linker_flags != NULL:
                    fprintf(get_stderr(), "%s: --linker-flags cannot be given multiple times (try \"%s --help\")\n", argv[0], argv[0])
                    exit(2)
                if argc-i < 2:
                    fprintf(get_stderr(), "%s: there must be a string of flags after --linker-flags (try \"%s --help\")\n", argv[0], argv[0])
                    exit(2)
                args.linker_flags = argv[i+1]
                i += 2

            # Options for debugging the compiler
            case "-v" | "-vv" | "-vvv" | "-vvvv" | "-vvvvv":
                args.verbosity += (strlen(argv[i]) as int) - 1
                i++
            case "--verbose":
                args.verbosity++
                i++
            case "--tokenize-only":
                set_mode(&args, CompilingMode.TokenizeOnly)
                i++
            case "--parse-only":
                set_mode(&args, CompilingMode.ParseOnly)
                i++
            case "--uvg-only":
                set_mode(&args, CompilingMode.UvgOnly)
                i++
            case _:
                if starts_with(argv[i], "-j") and is_ascii_digit(argv[i][2]) and 1 <= atoi(&argv[i][2]) and atoi(&argv[i][2]) <= 100:
                    args.parallelism = atoi(&argv[i][2])
                    i++
                elif argv[i][0] == '-':
                    fprintf(get_stderr(), "%s: unknown argument \"%s\" (try \"%s --help\")\n", argv[0], argv[i], argv[0])
                    exit(2)
                elif args.infile != NULL:
                    fprintf(get_stderr(), "%s: you can only pass one Jou file (try \"%s --help\")\n", argv[0], argv[0])
                    exit(2)
                else:
                    args.infile = argv[i++]

    if args.infile == NULL:
        fprintf(get_stderr(), "%s: missing Jou file name (try \"%s --help\")\n", argv[0], argv[0])
        exit(2)

    check_args(&args, optlevel_set)
    return args
