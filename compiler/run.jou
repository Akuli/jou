import "stdlib/str.jou"
import "stdlib/list.jou"
import "stdlib/mem.jou"
import "stdlib/io.jou"
import "stdlib/process.jou"

import "./command_line_args.jou"

if WINDOWS:
    import "./paths.jou"
else:
    import "../config.jou"


def quote_paths(paths: List[byte*]) -> byte*:
    size = 10L
    for p = paths.ptr; p < paths.end(); p++:
        size += strlen(*p) + 10

    result: byte* = malloc(size)
    strcpy(result, "")
    for p = paths.ptr; p < paths.end(); p++:
        # TODO: quote better?
        strcat(result, " \"")
        strcat(result, *p)
        strcat(result, "\"")

    return result


@public
def run_linker(objpaths: List[byte*], exepath: byte*, linker_flags: byte*) -> None:
    quoted_object_files = quote_paths(objpaths)

    command: byte*
    if WINDOWS:
        # Assume mingw with clang has been downloaded with windows_setup.sh.
        #
        # If this is a bootstrap compiler, the location of mingw is something
        # like ../../../mingw64 relative to jou.exe location, and it is passed
        # with an environment variable.
        mingw_dir = getenv("JOU_MINGW_DIR")
        if mingw_dir != NULL:
            # Environment variable given
            mingw_dir = strdup(mingw_dir)
            while strstr(mingw_dir, "/") != NULL:
                *strstr(mingw_dir, "/") = '\\'
        else:
            # Assume the folder that contains jou.exe also contains mingw64
            jou_exe = find_current_executable()
            asprintf(&mingw_dir, "%s\\mingw64", dirname(jou_exe))
            free(jou_exe)

        # Could also use clang, but gcc has less dependencies so we can make the Windows zips smaller.
        # Windows quoting is weird. The outermost quotes get stripped here.
        asprintf(&command, "\"\"%s\\bin\\gcc.exe\" %s -o \"%s\" %s\"", mingw_dir, quoted_object_files, exepath, linker_flags)
        free(mingw_dir)
    else:
        # Assume clang is installed and use it to link. Could use lld, but clang is needed anyway.
        asprintf(&command, "'%s' %s -o '%s' %s", JOU_CLANG_PATH, quoted_object_files, exepath, linker_flags)

    free(quoted_object_files)

    if command_line_args.verbosity >= 1:
        printf("Running linker: %s\n", command)

    if system(command) != 0:
        exit(1)
    free(command)


@public
def run_exe(exepath: byte*, valgrind: bool) -> int:
    command: byte*
    if WINDOWS:
        asprintf(&command, "\"%s\"", exepath)
        while strstr(command, "/") != NULL:
            *strstr(command, "/") = '\\'
    else:
        if valgrind:
            asprintf(&command, "valgrind -q --leak-check=full --show-leak-kinds=all --error-exitcode=1 '%s'", exepath)
        else:
            asprintf(&command, "'%s'", exepath)

    # Make sure that everything else shows up before the user's prints.
    fflush(stdout)
    fflush(stderr)

    ret = system(command)
    free(command)

    if ret == 0:
        return 0  # success
    else:
        return 1  # TODO: extract actual error code / return value
