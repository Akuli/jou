declare printf(fmt: byte*, ...) -> int

# c can't do this
def make_array() -> int[3]:
    return [4, 5, 6]

# Arrays are passed by value.
def do_nothing(a: int[3]) -> None:
    ++a[0]

# Use a pointer instead if you want to modify the array.
def increment(a: int*) -> None:
    ++a[0]

def main() -> int:
    foo: int[3]
    foo[0] = 1
    foo[1] = 2
    foo[2] = 3

    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 1 2 3

    foo = make_array()
    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 4 5 6

    foo = [
        4,
        '\x05',  # converts to int
        6,
    ]
    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 4 5 6

    do_nothing(foo)
    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 4 5 6

    increment(&foo[0])
    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 5 5 6

    increment(foo)  # foo casts implicitly to &foo[0]
    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 6 5 6

    increment(foo as int*)  # same cast explicitly
    printf("%d %d %d\n", foo[0], foo[1], foo[2])  # Output: 7 5 6

    # corner case: byte* <--> void* can be converted both ways, use byte*
    strings = ["hello", NULL, "world", NULL]
    printf("%s %s\n", strings[0], strings[2])  # Output: hello world
    # Output: strings[1] is NULL
    # Output: strings[3] is NULL
    for i = 0; i < 4; i++:
        if strings[i] == NULL:
            printf("strings[%d] is NULL\n", i)

    return 0
