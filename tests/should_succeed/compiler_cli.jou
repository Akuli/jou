import "stdlib/assert.jou"
import "stdlib/str.jou"
import "stdlib/mem.jou"
import "stdlib/process.jou"
import "stdlib/io.jou"


def run_jou(command: byte*) -> None:
    if WINDOWS:
        jou_exe = "jou.exe"
    else:
        jou_exe = "./jou"

    full_command = malloc(1000)
    sprintf(full_command, "%s %s", jou_exe, command)
    system(full_command)
    free(full_command)


# Find and print things like the following...
#
#   Reusing previously compiled file: examples/jou_compiled/hello/hello_36494a390f444fbd_c836422383205631.o
#
# ...but replace things like 36494a390f444fbd with "<hash>" as they are platform-specific.
def print_reusing_compiled_file_lines(path: byte*) -> None:
    f = fopen(path, "r")
    assert f != NULL

    line: byte[500]
    while fgets(line, sizeof(line) as int, f) != NULL:
        if starts_with(line, "Reusing"):
            p = &line[0]
            while *p != '\0':
                is_hash = True
                for i = 0; i < 16; i++:
                    if not (('0' <= p[i] and p[i] <= '9') or ('a' <= p[i] and p[i] <= 'f')):
                        is_hash = False
                        break
                if is_hash:
                    printf("<hash>")
                    p = &p[16]
                else:
                    putchar(*p++)

    fclose(f)
    fflush(get_stdout())  # ensure output appears in the right place, not buffered until end


def main() -> int:
    run_jou("")  # Output: <jouexe>: missing Jou file name (try "<jouexe> --help")
    run_jou("--update -O3")  # Output: <jouexe>: "--update" cannot be used with other arguments (try "<jouexe> --help")
    run_jou("--verbose")  # Output: <jouexe>: missing Jou file name (try "<jouexe> --help")
    run_jou("x.jou y.jou")  # Output: <jouexe>: you can only pass one Jou file (try "<jouexe> --help")
    run_jou("examples/hello.jou")  # Output: Hello World
    run_jou("-o")  # Output: <jouexe>: there must be a file name after -o (try "<jouexe> --help")
    run_jou("-o examples/hello.jou")  # Output: <jouexe>: the filename after -o should be an executable, not a Jou file (try "<jouexe> --help")
    run_jou("-O8 examples/hello.jou")  # Output: <jouexe>: unknown argument "-O8" (try "<jouexe> --help")
    run_jou("--lolwat")  # Output: <jouexe>: unknown argument "--lolwat" (try "<jouexe> --help")
    run_jou("lolwat.jou")  # Output: compiler error in file "lolwat.jou": cannot open file: No such file or directory
    run_jou("--linker-flags")  # Output: <jouexe>: there must be a string of flags after --linker-flags (try "<jouexe> --help")
    run_jou("--linker-flags x --linker-flags y")  # Output: <jouexe>: --linker-flags cannot be given multiple times (try "<jouexe> --help")
    run_jou("--tokenize-only --tokenize-only")  # Output: <jouexe>: --tokenize-only cannot be given multiple times (try "<jouexe> --help")
    run_jou("--tokenize-only -O1 examples/hello.jou > tmp/hello-tokens.txt")  # Output: <jouexe>: warning: -O1 does nothing when used together with --tokenize-only (see "<jouexe> --help")
    run_jou("--tokenize-only --valgrind examples/hello.jou > tmp/hello-tokens.txt")  # Output: <jouexe>: warning: --valgrind does nothing when used together with --tokenize-only (see "<jouexe> --help")
    run_jou("--tokenize-only --linker-flags foobar examples/hello.jou > tmp/hello-tokens.txt")  # Output: <jouexe>: warning: --linker-flags does nothing when used together with --tokenize-only (see "<jouexe> --help")

    # Test caching of compiled files. We just ran hello.jou so nothing should be recompiled.
    #
    # Output: Reusing previously compiled file: examples/jou_compiled/hello/hello_<hash>_<hash>.o
    # Output: Reusing previously compiled file: examples/jou_compiled/hello/io_<hash>_<hash>.o
    run_jou("-v examples/hello.jou > tmp/hello_out_v.txt")
    print_reusing_compiled_file_lines("tmp/hello_out_v.txt")

    # Test treating compiler warnings as errors
    # Output: compiler warning for file "unused_enum.jou", line 3: enum 'Foo' defined but not used
    # Output: compiler error: there were warnings and --fail-on-warnings was given
    # Output: compiler warning for file "unused_enum.jou", line 3: enum 'Foo' defined but not used
    # Output: compiler error: there were warnings and --fail-on-warnings was given
    if WINDOWS:
        system("cd tests\\should_succeed && ..\\..\\jou.exe --fail-on-warnings unused_enum.jou && echo This should not be shown")
        system("cd tests\\should_succeed && ..\\..\\jou.exe -o foo --fail-on-warnings unused_enum.jou && echo This should not be shown")
    else:
        system("cd tests/should_succeed && ../../jou --fail-on-warnings unused_enum.jou && echo This should not be shown")
        system("cd tests/should_succeed && ../../jou -o foo --fail-on-warnings unused_enum.jou && echo This should not be shown")

    # No output file should be created
    assert fopen("tests/should_succeed/foo", "r") == NULL
    assert fopen("tests/should_succeed/foo.exe", "r") == NULL

    # Output: Usage:
    # Output:   <jouexe> [options] file.jou  # Compile and run code
    # Output:   <jouexe> --help              # This message
    # Output:   <jouexe> --update            # Download and install the latest Jou
    # Output:
    # Output: Options for compiling:
    # Output:   -o OUTFILE           output an executable file, don't run the code
    # Output:   -O0/-O1/-O2/-O3      set optimization level (0 = no optimization, 1 = default, 3 = runs fastest)
    # Output:   --valgrind           use valgrind when running the code
    # Output:   --fail-on-warnings   fail compiling if there were any compiler warnings
    # Output:   --linker-flags "..." appended to the linker command, just like 'link' statements in code
    # Output:
    # Output: Options for debugging the compiler:
    # Output:   -v / --verbose    show briefly what the compiler is doing
    # Output:   -vv               show even more details
    # Output:   --tokenize-only   display the output of the tokenizer, do not compile further
    # Output:   --parse-only      display the AST (parse tree), do not compile further
    # Output:   --uvg-only        display Undefined Value Graphs, do not compile further
    run_jou("--help")

    # Test that double-verbose kinda works, without asserting the output in too much detail.
    # Usually stdlib/_... files are compiled, but not necessarily, so hide them.
    # Showing LLVM IR is not consistent because of caching compiled files.
    run_jou("-vv examples/hello.jou | grep === | grep -v stdlib/io.jou | grep -v \"LLVM IR\"")
    # Output: ===== Tokens for file "examples/hello.jou" =====
    # Output: ===== AST for file "examples/hello.jou" =====
    # Output: ===== UVG for main() -> int =====
    # Output: ===== UVG for get_stdin() -> FILE* =====
    # Output: ===== UVG for get_stdout() -> FILE* =====
    # Output: ===== UVG for get_stderr() -> FILE* =====

    # Different working directory.
    # Output: Hello World
    if WINDOWS:
        system("cd tests && ..\\jou.exe ../examples/hello.jou")
    else:
        system("cd tests && ../jou ../examples/hello.jou")

    # Compile to an executable.
    # Output: Hello World
    run_jou("-o tmp/tests/hello.exe examples/hello.jou")
    if WINDOWS:
        system("tmp\\tests\\hello.exe")
    else:
        system("tmp/tests/hello.exe")

    # Compiler in weird place
    # TODO: make this test work with new (more robust) standard library finding logic?
    #if WINDOWS:
    #    system("copy jou.exe tmp\\tests\\jou_executable.exe >nul")
    #    system("tmp\\tests\\jou_executable.exe")
    #else:
    #    system("cp jou tmp/tests/jou_executable")
    #    system("tmp/tests/jou_executable")

    # Compile a GUI program. Doesn't work on GitHub Actions because X11 isn't installed.
    if not WINDOWS and not MACOS and getenv("GITHUB_ACTIONS") == NULL:
        if NETBSD:
            ret = system("./jou -o /dev/null --linker-flags \"-L/usr/X11R7/lib -lX11\" examples/x11_window.jou")
        else:
            ret = system("./jou -o /dev/null --linker-flags \"-lX11\" examples/x11_window.jou")
        assert ret == 0

    # Compile a program with a memory leak.
    # Output: 100 bytes in 1 blocks are definitely lost in loss record 1 of 1
    if WINDOWS or system("which valgrind >/dev/null 2>/dev/null") != 0:
        # valgrind not available --> produce some fake output to pass the test
        puts("100 bytes in 1 blocks are definitely lost in loss record 1 of 1")
    else:
        ret = system("./jou --valgrind examples/memory_leak.jou 2>&1 | grep 'definitely lost' | cut -d' ' -f2-")
        assert ret == 0

    return 0
