from "stdlib/str.jou" import sprintf, strstr
from "stdlib/io.jou" import printf
from "stdlib/mem.jou" import malloc, free
from "stdlib/process.jou" import system, getenv

def is_windows() -> bool:
    return getenv("OS") != NULL and strstr(getenv("OS"), "Windows") != NULL

def run_jou(command: byte*) -> void:
    if is_windows():
        jou_exe = "jou.exe"
    else:
        jou_exe = "./jou"

    full_command = malloc(1000)
    sprintf(full_command, "%s %s", jou_exe, command)
    system(full_command)
    free(full_command)

def main() -> int:
    run_jou("")  # Output: <jouexe>: missing file name to run (try "<jouexe> --help")
    run_jou("--update -O3")  # Output: <jouexe>: "--update" cannot be used with other arguments (try "<jouexe> --help")
    run_jou("--verbose")  # Output: <jouexe>: missing file name to run (try "<jouexe> --help")
    run_jou("x.jou y.jou")  # Output: <jouexe>: you can only pass one Jou file (try "<jouexe> --help")
    run_jou("examples/hello.jou")  # Output: Hello World
    run_jou("-O8 examples/hello.jou")  # Output: <jouexe>: unknown argument "-O8" (try "<jouexe> --help")
    run_jou("--lolwat")  # Output: <jouexe>: unknown argument "--lolwat" (try "<jouexe> --help")
    run_jou("lolwat.jou")  # Output: compiler error in file "lolwat.jou": cannot open file: No such file or directory

    # Output: Usage:
    # Output:   <jouexe> [--verbose] [-O0|-O1|-O2|-O3] FILENAME
    # Output:   <jouexe> --help       # This message
    # Output:   <jouexe> --update     # Download and install the latest Jou
    # Output:
    # Output: Options:
    # Output:   --help           display this message
    # Output:   --verbose        display a lot of information about all compilation steps
    # Output:   -O0/-O1/-O2/-O3  set optimization level (0 = default, 3 = runs fastest)
    run_jou("--help")

    # Test that --verbose kinda works, without asserting the output in too much detail.
    # See README for an explanation of why CFG is twice.
    # TODO: shouldn't need to hide stdlib/io.jou stuff, ideally it would be precompiled
    run_jou("--verbose examples/hello.jou | grep === | grep -v stdlib/io.jou")
    # Output: ===== Tokens for file "examples/hello.jou" =====
    # Output: ===== AST for file "examples/hello.jou" =====
    # Output: ===== Control Flow Graphs for file "examples/hello.jou" =====
    # Output: ===== Control Flow Graphs for file "examples/hello.jou" =====
    # Output: ===== Unoptimized LLVM IR for file "examples/hello.jou" =====

    # With optimizations enabled, we see also the optimized LLVM IR.
    run_jou("--verbose -O1 examples/hello.jou | grep 'LLVM IR for file'")
    # Output: ===== Unoptimized LLVM IR for file "<joudir>/stdlib/io.jou" =====
    # Output: ===== Optimized LLVM IR for file "<joudir>/stdlib/io.jou" =====
    # Output: ===== Unoptimized LLVM IR for file "examples/hello.jou" =====
    # Output: ===== Optimized LLVM IR for file "examples/hello.jou" =====

    # Different working directory.
    # Output: Hello World
    if is_windows():
        system("cd tests & ..\\jou.exe ../examples/hello.jou")
    else:
        system("cd tests && ../jou ../examples/hello.jou")

    # Executable in weird place
    # Output: error: cannot find the Jou standard library in <joudir>/tmp/tests/stdlib
    if is_windows():
        system("copy jou.exe tmp\\tests\\jou_executable.exe >nul")
        system("tmp\\tests\\jou_executable.exe")
    else:
        system("cp jou tmp/tests/jou_executable")
        system("tmp/tests/jou_executable")

    return 0
