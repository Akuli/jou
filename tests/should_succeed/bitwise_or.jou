# Tests for the bitwise OR operator (a | b)

import "stdlib/io.jou"


# These are used to fail compiling if a value is of the wrong type.
#
# Taking a pointer avoids implicit casts. For example, if b is a byte, then
# b implicitly casts to int16, but &b doesn't implicitly cast to int16*.
def check_int16(x: int16*) -> None:
    pass

def check_uint16(x: uint16*) -> None:
    pass


def main() -> int:
    printf("%#x\n", 0xf0 | 0x0f)  # Output: 0xff
    printf("%#x\n", 0xf1 | 0x0f)  # Output: 0xff
    printf("%#x\n", 0x11223344 | 0x01020304)  # Output: 0x11223344

    # Types of different sizes, result type is bigger type
    a = ('A' as byte) | (32 as int16)
    b = ('A' as byte) | (32 as uint16)
    c = ('A' as int8) | (32 as int16)
    d = ('A' as int8) | (32 as uint16)

    check_int16(&a)
    check_uint16(&b)
    check_int16(&c)
    check_uint16(&d)

    # Same types, use the type as is
    e = ('A' as int16) | (32 as int16)
    f = ('A' as uint16) | (32 as uint16)
    check_int16(&e)
    check_uint16(&f)

    # Same size but different signed-ness, prefer unsigned
    g = ('A' as int16) | (32 as uint16)
    h = ('A' as uint16) | (32 as int16)
    check_uint16(&g)
    check_uint16(&h)

    # Output: aaaaaaaa
    printf("%c%c%c%c%c%c%c%c\n", a, b, c, d, e, f, g, h)

    # In-place operator
    s: byte[10] = "HeLlO"
    for i = 0; i < 5; i++:
        s[i] |= 32
    puts(s)  # Output: hello

    return 0
