from "stdlib/mem.jou" import malloc, realloc, free, memset
from "stdlib/str.jou" import strlen, sprintf, strstr, strdup
from "stdlib/io.jou" import fprintf, stderr
from "stdlib/process.jou" import exit, getenv

declare GetModuleFileNameA(hModule: void*, lpFilename: byte*, nSize: int) -> int
declare readlink(linkpath: byte*, result: byte*, result_size: long) -> long

def is_windows() -> bool:
    # TODO: this is just weird...
    return getenv("OS") != NULL and strstr(getenv("OS"), "Windows") != NULL

# Return a path to the currently running program. Return value must be free()d.
def find_current_executable() -> byte*:
    buf = NULL
    for size = 2L; True; size *= 2:
        buf = realloc(buf, size)
        memset(buf, 0, size)

        if is_windows():
            ret: long = GetModuleFileNameA(NULL, buf, size as int)
        else:
            ret = readlink("/proc/self/exe", buf, size)

        if ret <= 0:
            # TODO: include os error message (GetLastError / errno)
            fprintf(stderr, "error: cannot locate currently running executable, needed for finding the Jou standard library\n")
            exit(1)
        if ret < size:
            # buffer is big enough, it fits
            return buf

# TODO: move to stdlib
declare dirname(path: byte*) -> byte*
declare stat(path: byte*, buf: byte[1000]*) -> int  # lol

def find_stdlib() -> byte*:
    checked: byte*[3]
    memset(&checked, 0, sizeof checked)

    exedir = find_current_executable()
    while is_windows() and strstr(exedir, "\\") != NULL:
        *strstr(exedir, "\\") = '/'

    for i = 0; i < sizeof checked / sizeof checked[0]; i++:
        tmp = strdup(dirname(exedir))
        free(exedir)
        exedir = tmp

        if strlen(exedir) <= 3:
            # give up, seems like we reached root of file system (e.g. "C:/" or "/")
            break

        path = malloc(strlen(exedir) + 10)
        sprintf(path, "%s/stdlib", exedir)

        iojou: byte* = malloc(strlen(path) + 10)
        sprintf(iojou, "%s/io.jou", path)
        buf: byte[1000]
        stat_result = stat(iojou, &buf)
        free(iojou)

        if stat_result == 0:
            free(exedir)
            return path

        checked[i] = path

    # TODO: test this
    fprintf(stderr, "error: cannot find the Jou standard library in any of the following locations:\n")
    for i = 0; i < sizeof checked / sizeof checked[0] and checked[i] != NULL; i++:
        fprintf(stderr, "  %s\n", checked[i])
    exit(1)
    return NULL  # TODO: never runs, but compiler shows warning if I delete this
